plugins {
	id 'application'
	id 'java'
	id 'com.google.osdetector' version '1.7.3'
	id 'com.gradleup.shadow' version '9.3.1'
}

group 'org.example'
version '1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

ext {
	javaMainClass = 'org.example.ServerApplication'

	os_suffix = ""
	if (osdetector.classifier in ['linux-x86_64'] || ['osx-x86_64'] || ['osx-aarch_64'] || ['windows-x86_64']) {
		os_suffix = ":" + osdetector.classifier
	}

	reactorCoreDefaultVersion = '3.8.2'
	if (!project.hasProperty('forceReactorCoreVersion')) {
		reactorCoreVersion = reactorCoreDefaultVersion
	}
	else {
		reactorCoreVersion = forceReactorCoreVersion
		println "Reactor Core version is configured from command line: ${forceReactorCoreVersion}"
	}

	reactorNettyDefaultVersion = '1.3.2'
	if (!project.hasProperty('forceReactorNettyVersion')) {
		reactorNettyVersion = reactorNettyDefaultVersion
	}
	else {
		reactorNettyVersion = forceReactorNettyVersion
		println "Reactor Netty version is configured from command line: ${forceReactorNettyVersion}"
	}

	nettyVersion = '4.2.9.Final'
	boringSslVersion = '2.0.74.Final'
	bouncycastleVersion = '1.83'

	jacksonDatabindVersion = '2.21.0'

	logbackVersion = '1.2.13'
}

repositories {
	mavenLocal()
	maven { url 'https://repo.spring.io/snapshot' }
	mavenCentral()
}

dependencies {
	implementation "io.projectreactor:reactor-core:${reactorCoreVersion}"
	implementation "io.projectreactor.netty:reactor-netty-core:${reactorNettyVersion}"
	implementation "io.projectreactor.netty:reactor-netty-http:${reactorNettyVersion}"

	implementation "io.netty:netty-tcnative-boringssl-static:${boringSslVersion}${os_suffix}"
	implementation "io.netty:netty-pkitesting:$nettyVersion"
	implementation "org.bouncycastle:bcpkix-jdk18on:${bouncycastleVersion}"

	implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"

	implementation "ch.qos.logback:logback-classic:${logbackVersion}"
}

application {
	mainClass = javaMainClass

	if (project.hasProperty('http2')) {
		println "HTTP/2 is enabled"
		applicationDefaultJvmArgs += ['-Dhttp2']
	}

	if (project.hasProperty('host')) {
		println "The server host is configured to be ${host}"
		applicationDefaultJvmArgs += ["-Dhost=${host}"]
	}

	if (project.hasProperty('port')) {
		println "The server port is configured to be ${port}"
		applicationDefaultJvmArgs += ["-Dport=${port}"]
	}

	if (project.hasProperty('remoteServer')) {
		println "The remote server is configured to be ${remoteServer}"
		applicationDefaultJvmArgs += ["-DremoteServer=${remoteServer}"]
	}
}